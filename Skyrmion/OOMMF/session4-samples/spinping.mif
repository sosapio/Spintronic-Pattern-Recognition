# MIF 2.1
# Current induced domain wall propagation in a Permalloy nanowire with a 160nm×8nm cross section

set pi [expr {4*atan(1.)}]
set mu0 [expr {4*$pi*1e-7}]

Parameter stage_time 0.005 ;# Stage length in ns
Parameter run_time  10.  ;# Total run time in ns
set number_of_stages [expr {int(ceil($run_time/double($stage_time)))}]
set stage_time [expr {$stage_time*1e-9}]

Parameter u_max       1000 ;# Max u, m/s
Parameter pulse_period 0.1 ;# Pulse sine period, in ns
Parameter pulse_offset 0.5 ;# Pulse offset, in ns

set pulse_scale [expr {2*$pi*1e9/$pulse_period}]
set pulse_offset [expr {$pulse_offset*1e-9}]


set length 5.0E-6
set width 160E-9
set thickness 8e-9
set Ms 800e3

Specify Oxs_ImageAtlas:atlas [subst {
  xrange {0 $length}
  yrange {0 $width}
  zrange {0 $thickness }
  image spinping-mask.gif
  viewplane xy
  colormap {
   white nonmagnetic
   black magnetic
	red fixed
  }
}]

Specify Oxs_RectangularMesh:mesh {
  cellsize {4e-9 4e-9 8e-9}
  atlas :atlas
}

Specify Oxs_UniformExchange {
  A  13E-12
}

Specify Oxs_FileVectorField:leftedge {
   file "spinping-edge.ohf"
   spatial_scaling {1 1 1}
   spatial_offset  {0 0 0}
}

#Field from the left edge
Specify Oxs_FixedZeeman:leftadj { 
   field :leftedge
   multiplier -1
}

# Field from the right edge.  This is the same as the left edge field,
# but rotated about the origin via the 'M -1' entry (the *.ohf fields
# are located in the first octant), and translated into position with
# the offset entry.
Specify Oxs_FixedZeeman:rightadj [subst { 
   field { Oxs_AffineOrientVectorField {
      field :leftedge
      M -1
      offset { $length $width $thickness }
   }}
}]

Specify Oxs_Demag {}

proc Sinc { t } {
   if {abs($t)<1e-6} {
      set v [expr {1-$t*$t/6.}]
   } else {
      set v [expr {sin($t)/$t}]
   }
   return $v
}
proc SincPulse { total_time } {
   global pulse_scale pulse_offset
   set t [expr {$total_time - $pulse_offset}]
   set st [expr {$t*$pulse_scale}]
   return [Sinc $st]
}

#Evolver with spin-torque/spin-flip terms
# For Permalloy u=J*P*7e-11 (Thiaville-epl)
Specify Anv_SpinTEvolve [subst {
  do_precess 1
  gamma_LL 2.21e5
  method rkf54s
  alpha 0.005
  fixed_spins {
  	atlas fixed
  }
  u $u_max
  u_profile SincPulse
  u_profile_args total_time
  beta 0.04
}]

Specify Oxs_TimeDriver [subst {
 evolver Anv_SpinTEvolve
 mesh :mesh
 stopping_time $stage_time
 stage_count $number_of_stages
 Ms {Oxs_AtlasScalarField {
   atlas :atlas
   values {
      nonmagnetic  0
      magnetic     $Ms
      fixed 	    $Ms
   }}}
 m0 { Oxs_FileVectorField {
   atlas :atlas
   norm  1
   file ./spinping-init.omf
}}
}]
